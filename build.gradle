plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.0'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'cz.dan'
version = '0.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

compileJava {
    options.compilerArgs = ['-Amapstruct.defaultComponentModel=spring']
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/moravianblackcat/notes")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("ACTOR")
            password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
        }
    }
}

ext {
    set('assertjVersion', '3.22.0')
    set('cucumberVersion', '7.20.1')
    set('jacksonVersion', '2.18.0')
    set('junitVersion', '5.10.0')
    set('lombokVersion', '1.18.38')
    set('lombokMapstructBindingVersion', '0.2.0')
    set('log4jVersion', '2.17.1')
    set('mapStructVersion', '1.6.3')
    set('wiremockVersion', "2.35.1")
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"

    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapStructVersion}"
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "org.mapstruct:mapstruct:${mapStructVersion}"
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"

    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.cucumber:cucumber-java'
    testImplementation 'io.cucumber:cucumber-spring'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine'
    testImplementation 'org.junit.platform:junit-platform-suite'
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testImplementation 'org.awaitility:awaitility:4.2.1'
    testImplementation "cz.dan:common-helpers:1.0.0"
    testImplementation "cz.dan:integration-tests:2.0.5"
}

dependencyManagement {
    imports {
        mavenBom "io.cucumber:cucumber-bom:${cucumberVersion}"
        mavenBom "org.junit:junit-bom:${junitVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}


configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

tasks.named('test') {
    useJUnitPlatform()
    exclude '**/RunCucumberTests.class'
}

tasks.register('integrationTests', JavaExec) {
    dependsOn assemble, testClasses
    mainClass = "io.cucumber.core.cli.Main"
    classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
    args = [
            '--plugin', 'pretty',
            '--plugin', 'html:build/reports/cucumber-report.html',
            '--glue', 'cz.dan.notes.integration',
            'src/test/resources/features'
    ]
}
